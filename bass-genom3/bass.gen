/*/Copyright (c) 2014, LAAS/CNRS
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice,
 *    this list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 *
 * 3. Neither the name of the copyright holder nor the names of its
 *    contributors may be used to endorse or promote products derived from this
 *    software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

#include "bassStruct.idl"
#include "bassInterface.gen"

component bass {
    version         "1.0";
    lang            "c";
    require         "genom3 >= 2.99.24";
    provides        bassInterface;
    codels-require  "alsa >= 1.0.25";

    /* ---IDS declaration--- */
    native captureStruct;
    ids {
        captureStruct  cap;
    };

    /* ---Exceptions declaration--- */
    exception e_nomem, e_nodevice, e_device, e_hwparams, e_swparams;

    /* ---Task declaration--- */
    task capture;

    task socket {
        period      5ms;
        priority    200;
        stack       4000;

        codel <start>   sInitModule() yield ether;
    };

    /* ---Services declaration--- */
    function ListDevices() {
        doc     "Print available devices for sound capture on standard output";

        codel   codelListDevices();
        throw   e_nodevice;
    };

    activity Acquire(
      in string device = "hw:1,0" : "ALSA name of the sound device",
      in unsigned long sampleRate = 44100 : "Sample rate in Hz",
      in unsigned long nFramesPerChunk = 2205 : "Chunk size in frames",
      in unsigned long nChunksOnPort = 20 : "Port size in chunks"
      ) {
        doc     "Acquire data from an audio device and stream it on a port";
        task    capture;
        interrupts  Acquire;

        codel <start>      startAcquire(local in device, local in sampleRate,
                               local in nFramesPerChunk, local in nChunksOnPort,
                               inout ::ids, port out Audio) yield exec, ether;
        async codel <exec> execAcquire(inout ::ids, port out Audio)
                               yield exec, stop;
        codel <stop>       stopAcquire(inout ::ids) yield ether;

        throw   e_nomem, e_device, e_hwparams, e_swparams;
    };

    function Stop() {
        doc         "Stop acquisition of audio data";
        interrupts  Acquire;
    };

    activity DedicatedSocket() {
        codel <start>           initModule(port out Audio) yield ether, recv;
        async codel <recv>      Transfer(port out Audio) yield recv, ether;

        task    socket;
	};

    activity CloseSocket(){
        codel <start>           closeSocket() yield ether;
        task socket;
    };
};
